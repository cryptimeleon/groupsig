plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'org.cryptimeleon'
version '0.0.1' + (Boolean.valueOf(System.getProperty("disablesnapshot")) ? "" : "-SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {

    implementation group: 'org.cryptimeleon', name: 'craco', version: '1.0.0' + (Boolean.valueOf(System.getProperty("disablesnapshot")) ? "" : "-SNAPSHOT")
    implementation group: 'org.cryptimeleon', name: 'math', version: '1.0.0' + (Boolean.valueOf(System.getProperty("disablesnapshot")) ? "" : "-SNAPSHOT")


    // For tests
    testImplementation(group: 'org.cryptimeleon', name: 'craco', version:'1.0.0' + (Boolean.valueOf(System.getProperty("disablesnapshot")) ? "" : "-SNAPSHOT")) {
        capabilities {
            requireCapability("org.cryptimeleon:craco-tests")
        }
    }
    testCompileOnly(
            'junit:junit:4.12'
    )
    testImplementation(
            'org.junit.jupiter:junit-jupiter-api:5.7.0',
            'org.junit.jupiter:junit-jupiter-params:5.7.0',
            'org.reflections:reflections:0.9.10'
    )
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.7.0',
            'org.junit.vintage:junit-vintage-engine:5.7.0'
    )
}

test {
    useJUnitPlatform()
    maxParallelForks 4
    //we want display the following test events
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED"
    }
    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def startItem = '|  ', endItem = '  |'
            def repeatLength = startItem.length() + output.length() + endItem.length()
            println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
        }
    }
}
